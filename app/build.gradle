apply plugin: 'com.android.application'
apply from: 'MobSDK.gradle'

// http://www.race604.com/android-auto-version/
// use git count as build number, as current date as build version
static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yy.MM.dd')
    return formattedDate
}
def cmd = 'git rev-list HEAD --count'

def gitVersion = 2000+cmd.execute().text.trim().toInteger() //Starting 2000
def appVersion = getDate()

android {
    defaultConfig {
        applicationId "com.zfdang.zsmth_android"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode gitVersion
        versionName appVersion
        compileSdk rootProject.compileSdkVersion
        // Enabling multidex support.
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file("zsmth.release.jks")
            storePassword "zsmthdev"
            keyAlias "zsmth-release"
            keyPassword "zsmthdev"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    // http://www.sollyu.com/android-apk-studio-generated-automatically-appends-a-version-number/
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            if(buildType.name == "release") {
                variant.getPackageApplicationProvider().get().outputDirectory = new File(
                      project.rootDir.absolutePath+"/Releases/" )
                output.outputFileName = new File("zSMTH-v" + "-" + defaultConfig.versionName +"-"+ defaultConfig.versionCode+ ".apk" )
            } else {
                output.outputFileName = new File("zSMTH-v" + "-" + defaultConfig.versionName + ".apk" )
            }
        }

    }
    // ...
    testOptions {
        unitTests.returnDefaultValues = true
    }

    /*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    */

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }
// http://stackoverflow.com/questions/37586800/android-gradle-duplicate-files-copied-in-apk-meta-inf-license-txt
    lint {
        abortOnError false
    }
    namespace 'com.zfdang.zsmth_android'

}

dependencies {
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    final OKHTTP_VERSION = '5.1.0'
    final RETROFIT_VERSION = '3.0.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Fresco
    implementation "com.facebook.fresco:fresco:$rootProject.FRESCO_VERSION"
    // For animated GIF support
    implementation "com.facebook.fresco:animated-gif:$rootProject.FRESCO_VERSION"
    // For WebP support, including animated WebP
    implementation "com.facebook.fresco:webpsupport:$rootProject.FRESCO_VERSION"
    implementation "com.facebook.fresco:animated-webp:$rootProject.FRESCO_VERSION"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$rootProject.FRESCO_VERSION"

    // Support library1
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    // http://stackoverflow.com/questions/9783368/alternatives-to-preferencefragment-with-android-support-v4
    // https://github.com/Gericop/Android-Support-Preference-V7-Fix
    //noinspection GradleDependency
    implementation 'androidx.preference:preference:1.0.0'

    // RX
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.RXJAVA2_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.RXANDROID2_VERSION"

    // (Java only)
    implementation "androidx.work:work-runtime:2.10.3"
    // optional - RxJava2 support
    implementation "androidx.work:work-rxjava2:2.10.3"
    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-scalars:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    // umeng analytics
    implementation  'com.umeng.sdk:common:1.5.3'
    implementation  'com.umeng.sdk:analytics:7.5.3'

    // other dependencies
    implementation 'com.github.maksim88:PasswordEditText:0.9'
    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    implementation 'me.relex:circleindicator:2.1.6'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    //noinspection NewerVersionAvailable
    implementation('com.mikepenz:aboutlibraries:6.1.1@aar') {
        transitive = true
    } // do not upgrade, otherwise it will cause issue

    implementation 'com.oguzdev:CircularFloatingActionMenu:1.0.2'
    implementation 'com.github.gzu-liyujiang.AndroidPicker:WheelView:4.1.5'
    implementation  'io.github.scwang90:refresh-layout-kernel:2.1.1'      //核心必须依赖
    implementation  'io.github.scwang90:refresh-header-classics:2.1.1'    //经典刷新头
    implementation  'io.github.scwang90:refresh-footer-classics:2.1.1'    //经典加载
    implementation 'com.klinkerapps:link_builder:2.0.5'
    implementation 'com.esotericsoftware:kryo:5.6.2'
    implementation 'com.github.nisrulz:screenshott:2.0.0'

    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.mimecraft:mimecraft:1.1.1'

    // my gradles
    implementation project(':multiple-images-selector')

}

//https://blog.csdn.net/ismartv_dagou2/article/details/127392262
tasks.configureEach { task ->
    if(task.name.contains("ReleaseApkListingFileRedirect"))
    {
        task.enabled = false
    }
}





